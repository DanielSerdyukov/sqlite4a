import com.jfrog.bintray.gradle.BintrayUploadTask

apply plugin: 'com.android.library'
apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.2"

    defaultConfig {
        minSdkVersion 15
        targetSdkVersion 25
        versionCode 1
        versionName rootProject.version

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        externalNativeBuild {
            cmake {
                cppFlags "-std=c++11", "-fno-rtti", "-fno-exceptions", "-fvisibility=hidden"
                abiFilters "armeabi-v7a", "arm64-v8a", "x86"
            }
        }
    }

    externalNativeBuild {
        cmake {
            path "CMakeLists.txt"
        }
    }

    libraryVariants.all { variant ->
        int versionCode = 0
        (rootProject.version =~ /(\d+)/).collect {
            Integer.parseInt(it[0] as String)
        }.take(3).eachWithIndex { num, index ->
            versionCode += (num * Math.max(1, (1000000 / Math.pow(1000, index))))
        }
        variant.mergedFlavor.versionCode = versionCode
    }
}

configurations {
    androidTestCompile.exclude group: 'org.hamcrest', module: 'hamcrest-core'
}

dependencies {
    compile 'com.android.support:support-annotations:25.3.1'
    androidTestCompile 'com.getkeepsafe.relinker:relinker:1.2.1'
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
        exclude group: 'org.hamcrest'
    })
    androidTestCompile 'junit:junit:4.12'
    androidTestCompile 'org.hamcrest:java-hamcrest:2.0.0.0'
}

ext {
    pomXml = {
        resolveStrategy = DELEGATE_FIRST
        name project.name
        description project.description
        url 'https://github.com/DanielSerdyukov/sqlite4a'
        scm {
            url 'https://github.com/DanielSerdyukov/sqlite4a.git'
            connection 'scm:git:git://github.com/DanielSerdyukov/sqlite4a.git'
            developerConnection 'scm:git:git@github.com/DanielSerdyukov/sqlite4a.git'
        }
        licenses {
            license {
                name 'The Apache Software License, Version 2.0'
                url 'http://www.apache.org/license/LICENSE-2.0.txt'
                distribution 'repo'
            }
        }
        developers {
            developer {
                name 'Daniel Serdyukov'
            }
        }
    }
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.srcDirs
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId project.group
            artifactId project.name
            version project.version
            artifact "${project.buildDir}/outputs/aar/${project.name}-release.aar"
            artifact sourcesJar
            pom.withXml {
                asNode().children().last() + project.pomXml
                def dependencies = asNode().appendNode('dependencies')
                configurations.compile.allDependencies.each {
                    def dependency = dependencies.appendNode('dependency')
                    dependency.appendNode('groupId', it.group)
                    dependency.appendNode('artifactId', it.name)
                    dependency.appendNode('version', it.version)
                    dependency.appendNode('scope', 'compile')
                }
            }
        }
    }
}


bintray {
    user = properties.get('bintray.user')
    key = properties.get('bintray.apikey')
    publications = ['maven']
    pkg {
        repo = 'sqlite4a'
        name = 'sqlite4a'
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/DanielSerdyukov/sqlite4a.git'
        version {
            name = project.version
            released = new Date()
        }
    }
}

project.tasks.withType(PublishToMavenLocal).all { it.dependsOn 'assembleRelease' }
project.tasks.withType(BintrayUploadTask).all { it.dependsOn 'publishToMavenLocal' }
