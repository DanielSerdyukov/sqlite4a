apply plugin: 'com.jfrog.bintray'
apply plugin: 'maven-publish'

group = pJcenterGroup
version = pVersionName

task sourcesJar(type: Jar) {
    from files('src/main/java')
    classifier = 'sources'
}

artifacts {
    archives sourcesJar
}

publishing {
    publications {
        mavenAndroid(MavenPublication) {
            pom { packaging 'aar' }
            artifact file('build/outputs/aar/library-release.aar')
            artifact sourcesJar
            pom.withXml {
                Node root = asNode()
                root.appendNode('name', pJcenterArtifact)
                root.appendNode('description', 'SQLite bindings for Android')
                root.appendNode('url', pGitHubUrl)

                def issues = root.appendNode('issueManagement')
                issues.appendNode('system', 'github')
                issues.appendNode('url', "${pGitHubUrl}/issues")

                def scm = root.appendNode('scm')
                scm.appendNode('url', "scm:${pGitHubUrl}.git")
                scm.appendNode('connection', "scm:${pGitHubUrl}.git")

                def license = root.appendNode('licenses').appendNode('license')
                license.appendNode('name', 'The Apache Software License, Version 2.0')
                license.appendNode('url', 'http://www.apache.org/licenses/LICENSE-2.0.txt')
                license.appendNode('distribution', 'repo')

                def dependencies = root.appendNode('dependencies')
                ['compile'].each { scope ->
                    project.configurations[scope].allDependencies.each {
                        def dependency = dependencies.appendNode('dependency')
                        dependency.appendNode('groupId', it.group)
                        dependency.appendNode('artifactId', it.name)
                        dependency.appendNode('version', it.version)
                        dependency.appendNode('scope', scope)
                    }
                }
            }
        }
    }
}

bintray {
    user = System.properties['bintrayUser']
    key = System.properties['bintrayKey']
    configurations = ['archives']
    publications = ['mavenAndroid']
    publish = true
    pkg {
        repo = 'maven'
        name = pJcenterGroup
        licenses = ['Apache-2.0']
        version {
            name = pVersionName
            vcsTag = pVersionName
        }
    }
}
