apply plugin: 'com.android.library'

android {
    compileSdkVersion Integer.parseInt(pCompileSdkVersion)
    buildToolsVersion pBuildToolsVersion

    defaultConfig {
        minSdkVersion Integer.parseInt(pMinSdkVersion)
        targetSdkVersion Integer.parseInt(pTargetSdkVersion)
        versionCode 1
        versionName pVersionName

        consumerProguardFiles 'proguard.pro'

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        externalNativeBuild {
            cmake {
                arguments "-DANDROID_TOOLCHAIN=clang"
                cppFlags "-std=c++11", "-fno-rtti", "-fno-exceptions", "-fvisibility=hidden"
                abiFilters "armeabi-v7a", "arm64-v8a", "x86"
            }
        }
    }

    externalNativeBuild {
        cmake {
            path "CMakeLists.txt"
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard.pro'
        }
    }

    libraryVariants.all { variant ->
        int versionCode = 0
        (pVersionName =~ /(\d+)/).collect {
            Integer.parseInt(it[0] as String)
        }.take(4).eachWithIndex { num, index ->
            versionCode += (num * Math.max(1, (1000000 / Math.pow(100, index))))
        }
        variant.mergedFlavor.versionCode = versionCode
    }
}

dependencies {
    compile 'com.android.support:support-annotations:24.2.1'
    compile 'com.getkeepsafe.relinker:relinker:1.2.1'
    androidTestCompile 'com.android.support.test.espresso:espresso-core:2.2.2'
    androidTestCompile 'com.android.support.test:runner:0.5'
}

apply from: 'bintray.gradle'
